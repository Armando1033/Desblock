apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 20
        versionCode 35
        versionName "2.8.12"
    }

    signingConfigs {
        release {
            storeFile System.getenv().containsKey('ANDROID_KEYSTORE') ? file(System.getenv('ANDROID_KEYSTORE')) : file(".")
            storePassword ""
            keyAlias System.getenv().containsKey('ANDROID_KEYSTORE_ALIAS') ? System.getenv('ANDROID_KEYSTORE_ALIAS').replace("\"", "") : ""
            keyPassword ""
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile project(':libraries:ActiveAndroid')
    compile project(':libraries:PebbleKit')
    compile 'com.google.android.gms:play-services:6.1.11'
    compile 'fr.nicolaspomepuy:discreetapprate:2.0@aar'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.github.tony19:logback-android-core:1.1.1-3'
    compile 'com.github.tony19:logback-android-classic:1.1.1-3'
    compile 'org.slf4j:slf4j-api:1.7.6'

    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.1'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.1'
}


// borrowed from https://www.timroes.de/2014/01/19/using-password-prompts-with-gradle-build-files/
// and https://www.timroes.de/2013/09/22/handling-signing-configs-with-gradle/
gradle.taskGraph.whenReady { taskGraph ->
    // Only execute when we are trying to assemble a release build
    if(taskGraph.hasTask(':pebblelocker:assembleRelease') || taskGraph.hasTask(':pebblelocker:installRelease')) {
        def password = System.console().readPassword("\nPlease enter key passphrase: ")
        if(password.size() <= 0) {
            throw new InvalidUserDataException("You must enter a password to proceed.")
        }

        // Must create String because System.readPassword() returns char[]
        // (and assigning that below fails silently)
        password = new String(password)

        android.signingConfigs.release.storePassword = password
        android.signingConfigs.release.keyPassword = password
    }
}
