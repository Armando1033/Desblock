apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 41
        versionName "4.0.2-beta"
    }

    // required to fix build failures on travis
    adbOptions {
      timeOutInMs 30000 // 30 seconds
    }

    signingConfigs {
        release {
            storeFile file(System.properties['KEYSTORE'])
            storePassword ""
            keyAlias System.properties['KEYSTORE_EMAIL']
            keyPassword ""
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.getpebble:pebblekit:3.0.0@aar'
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    compile 'com.google.android.gms:play-services-wearable:8.3.0'
    compile 'fr.nicolaspomepuy:discreetapprate:2.0@aar'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.lukekorth:mailable_log:0.1.3'

    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.1'
}


// borrowed from https://www.timroes.de/2014/01/19/using-password-prompts-with-gradle-build-files/
// and https://www.timroes.de/2013/09/22/handling-signing-configs-with-gradle/
gradle.taskGraph.whenReady { taskGraph ->
    // Only execute when we are trying to assemble a release build
    if(taskGraph.hasTask(':pebblelocker:assembleRelease') || taskGraph.hasTask(':pebblelocker:installRelease')) {
        def password = System.console().readPassword("\nPlease enter key passphrase: ")
        if(password.size() <= 0) {
            throw new InvalidUserDataException("You must enter a password to proceed.")
        }

        // Must create String because System.readPassword() returns char[]
        // (and assigning that below fails silently)
        password = new String(password)

        android.signingConfigs.release.storePassword = password
        android.signingConfigs.release.keyPassword = password
    }
}
